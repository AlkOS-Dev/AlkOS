#
# This module provides a function to register a complete, runnable
# environment for a given architecture.
#

include(ValidationHelpers)

#===============================================================================
# alkos_register_runtime_environment
#===============================================================================
#
# This function registers a complete runtime environment for a specific architecture.
# It creates targets for building an ISO, running the OS in QEMU, and optionally
# running tests or debugging with GDB.
#
## Parameters:
#   ARCH_NAME             The name of the architecture (e.g., x86_64).
#   BOOTABLE_EXECUTABLE   The bootloader or initial kernel executable target.
#   QEMU_COMMAND          The QEMU command for the architecture (e.g., qemu-system-x86_64).
#   QEMU_NORMAL_FLAGS     Default flags for running the OS in QEMU.
#   QEMU_TEST_FLAGS       Specific flags for running tests in QEMU.
## Keyword Arguments:
#   MODULES               A list of kernel module targets to include in the ISO.
#   MODULE_COMMANDS       A list of corresponding commands for the bootloader menu.
## Example:
#   alkos_register_runtime_environment(
#       ARCH_NAME           x86_64
#       BOOTABLE_EXECUTABLE alkos.loader32
#       QEMU_COMMAND        "qemu-system-x86_64"
#       QEMU_NORMAL_FLAGS   "-m 4G -smp 4"
#       QEMU_TEST_FLAGS     "-m 4G -display none"
#       MODULES
#           alkos.loader64
#           alkos.kernel
#       MODULE_COMMANDS
#           "loader64"
#           "kernel"
#   )
#
function(alkos_register_runtime_environment)
    set(options)
    set(oneValueArgs
        ARCH_NAME
        BOOTABLE_EXECUTABLE
        QEMU_COMMAND
        QEMU_NORMAL_FLAGS
        QEMU_TEST_FLAGS
    )
    set(multiValueArgs
        MODULES
        MODULE_COMMANDS
    )

    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    set(MAKE_ISO_SCRIPT_PATH ${CMAKE_SOURCE_DIR}/../scripts/actions/make_iso.bash)
    set(RUN_ALKOS_SCRIPT_PATH ${CMAKE_SOURCE_DIR}/../scripts/actions/run_alkos.bash)
    set(RUN_TESTS_SCRIPT_PATH ${CMAKE_SOURCE_DIR}/../scripts/actions/run_tests.bash)
    set(ALKOS_ISO_PATH ${CMAKE_BINARY_DIR}/alkos-${ARG_ARCH_NAME}.iso)
    alkos_ensure_path_exists(
        PATHS ${MAKE_ISO_SCRIPT_PATH} ${RUN_ALKOS_SCRIPT_PATH} ${RUN_TESTS_SCRIPT_PATH}
    )

    # This is how you pass list arguments to a bash script from CMake
    string(REPLACE ";" " " KERNEL_MODULES_FORMATTED "${ARG_MODULES}")
    string(REPLACE ";" " " KERNEL_COMMANDS_FORMATTED "${ARG_MODULE_COMMANDS}")

    # Write a bash config file specific to this architecture
    set(BASH_CONF_FILE "${CMAKE_SOURCE_DIR}/../config/conf.generated.bash")

    file(WRITE "${BASH_CONF_FILE}" "#!/bin/bash\n")
    file(APPEND "${BASH_CONF_FILE}" "# Autogenerated config for ${ARG_ARCH_NAME}\n")
    file(APPEND "${BASH_CONF_FILE}" "# Do not try to edit this by hand!\n")
    file(APPEND "${BASH_CONF_FILE}" "\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_ARCH=\"${ARG_ARCH_NAME}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_BUILD_TYPE=\"${CMAKE_BUILD_TYPE}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_SYSROOT=\"${CMAKE_SYSROOT}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_BOOTABLE_KERNEL_EXEC=\"${ARG_BOOTABLE_EXECUTABLE}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_ISO_PATH=\"${ALKOS_ISO_PATH}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_KERNEL_MODULES=\"${KERNEL_MODULES_FORMATTED}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_KERNEL_COMMANDS=\"${KERNEL_COMMANDS_FORMATTED}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_BUILD_DIR=\"${CMAKE_BINARY_DIR}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_TOOL_DIR=\"${TOOL_BINARIES_DIR}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_QEMU_COMMAND=\"${ARG_QEMU_COMMAND}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_QEMU_NORMAL_FLAGS=\"${ARG_QEMU_NORMAL_FLAGS}\"\n")
    file(APPEND "${BASH_CONF_FILE}" "export CONF_QEMU_TEST_FLAGS=\"${ARG_QEMU_TEST_FLAGS}\"\n")

    add_custom_target(iso-${ARG_ARCH_NAME}
        COMMAND ${MAKE_ISO_SCRIPT_PATH} -v
        DEPENDS ${ARG_BOOTABLE_EXECUTABLE} ${ARG_MODULES} 
        WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
        COMMENT "Building ISO for ${ARG_ARCH_NAME}"
    )

    add_custom_target(run-${ARG_ARCH_NAME}
        COMMAND ${RUN_ALKOS_SCRIPT_PATH} -v
        DEPENDS iso-${ARG_ARCH_NAME}
        WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
        COMMENT "Running AlkOS for ${ARG_ARCH_NAME}"
    )

    add_custom_target(run-with-gdb-${ARG_ARCH_NAME}
        COMMAND ${RUN_ALKOS_SCRIPT_PATH} -v --gdb 
        DEPENDS iso-${ARG_ARCH_NAME}
        WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
        COMMENT "Running AlkOS with GDB for ${ARG_ARCH_NAME}"
    )

    add_custom_target(mount-${ARG_ARCH_NAME}
        COMMAND ${RUN_ALKOS_SCRIPT_PATH} -v --mount 
        DEPENDS iso-${ARG_ARCH_NAME}
        WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
        COMMENT "Mounting AlkOS for ${ARG_ARCH_NAME}"
    )

    if (CMAKE_FEATURE_FLAG_RUN_TEST_MODE)
        separate_arguments(SPLIT_TEST_ARGS UNIX_COMMAND "${CMAKE_TEST_ARGS}")
        add_custom_target(tests-${ARG_ARCH_NAME}
          COMMAND ${RUN_TESTS_SCRIPT_PATH} ${SPLIT_TEST_ARGS}
          DEPENDS iso-${ARG_ARCH_NAME}
          WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/../scripts/tests
          COMMENT "Running tests for AlkOS on ${ARG_ARCH_NAME}"
      )
    endif()
endfunction()
